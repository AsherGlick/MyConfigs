fpath=( ~/zsh/completion $fpath )



typeset -ga preexec_functions # set a global array named preexex_functions
typeset -ga precmd_functions
typeset -ga chpwd_functions
setopt prompt_subst

################################################################################
############################ CONFIGURABLE SETTIGNS #############################
################################################################################

# Any commands that run for longer then this number of seconds will get a
# execution time printed to the terminal uppon completion
HIDE_TIME_THRESHOLD=5
NOTIFY_TIME_THRESHOLD=60





################################################################################
############################# PROMPT CONFIGURATION #############################
################################################################################
autoload -Uz promptinit
promptinit
autoload -U colors && colors

bold() {
  echo "%B$1%b"
}
underline() {
  echo "%U$1%u"
}
bold_underline() {
  echo $(bold $(underline $1))
}
forground() {
  echo %{$fg[$1]%}$2
}

n="
"
########################## CUSTOM PROMPT CONFIGURATION #########################
# %d Your current working directory relative to /home/                         #
# %~ Your current working directory relative to /home/member/u/username        #
# %B %b Begin and end bold print                                               #
# %U %u Begin and end underlined print                                         #
# %M server name e.g: prodigy.redbrick.dcu.ie                                  #
# %m The part of the hostname up to the first dot e.g: prodigy                 #
# %n Your login name                                                           #
# %T system time in HH:mm format                                               #
# %* system time in HH:mm:ss format                                            #
# %D Todays date in YY-MM-DD format                                            #
# %l Your current tty e.g: pts/100                                             #
################################################################################

# Get the name of the current screen session (if there is one)
screen_prompt=""
if [[ -n "$STY" ]]
then
  screen_color="%{$bg[cyan]$fg[black]%}"
  screen_name=`echo "$STY" | cut -d '.' -f 2`
  screen_prompt=$screen_color$screen_name%{$reset_color%}
fi

# Get the user and machine values and color
username=$(bold $(forground green '%n'))
machine_name=$(bold $(forground green '%m'))
open_bracket=$(forground white '[')
close_bracket=$(forground white ']')
at_sign=$(forground white '@')
machine_prompt=$open_bracket$username$at_sign$machine_name$close_bracket%{$reset_color%}

# Get the current path
current_path=$(bold $(forground blue '%~'))

# Get the current time
prompt_date=$( bold $(forground grey "%D{%H:%M:%S}"))


# the function that executes right before any ZSH command executes
function preexec() {
  # Start the timer to see when the command is executed
  timer=${timer:-$SECONDS}
  commandrun='Y'

  # Store the command run so it can be accessed easily in other commands
  PREVIOUS_COMMAND=$1
}


# the function that executes right before and ZSH prompt is displayed
function precmd() {
  resultcode=$?
  preprompt=""

  # if no command has been run dont display the error code again
  if [[ $commandrun == "Y" ]]; then
    commandrun='N'
  else
    resultcode=0
  fi

  # If there is an errorcode (not 0) then print it, otherwise don't
  if [ $resultcode -ne 0 ]; then
    preerror=$(forground red "$resultcode")
    preprompt="${preprompt} with Error $preerror$reset_color"
  fi

  # Output Runtime of previous command if it is longer then the threshold
  if [ $timer ]; then
    timer_show=$(($SECONDS - $timer))
    if [ $timer_show -gt $HIDE_TIME_THRESHOLD ]; then

      # Format the time
      pretime=$(forground cyan "$(format_seconds $timer_show)" )
      preprompt="${preprompt} in $pretime$reset_color s"
    fi
    unset timer
  fi

  if [[ "$preprompt" != "" ]]
  then
    print -rP "Command Executed$preprompt"
  fi

  # If the time is beyond a longer threshold issue a system notification on completion
  if [ $timer_show -gt $NOTIFY_TIME_THRESHOLD ]; then
    notify-send --icon=terminal "Long Running Command Finished" "Completed in $(format_seconds $timer_show)s
${PREVIOUS_COMMAND}"
    paplay /usr/share/sounds/KDE-Sys-App-Positive.ogg
  fi
}


# Set the full promt variable
PROMPT="$screen_prompt$prompt_date$machine_prompt$current_path%{$reset_color%}%# "


# Make the prompt refresh every second to update the time in $prompt_date
TMOUT=1
TRAPALRM() {
    zle reset-prompt
}


################################################################################
#################################### COLORS ####################################
################################################################################
# enable color support of ls and also add handy aliases
if [ -x /usr/bin/dircolors ]; then
  test -r ~/.dircolors && eval "$(dircolors -b ~/.dircolors)" || eval "$(dircolors -b)"
  alias ls='ls --color=auto'
  #alias dir='dir --color=auto'
  #alias vdir='vdir --color=auto'

  alias grep='grep --color=auto'
  alias fgrep='fgrep --color=auto'
  alias egrep='egrep --color=auto'
fi


################################################################################
############################### EXTERNAL SOURCES ###############################
################################################################################
source ~/.zshrc_git # source all the git specific commands
source ~/.zshrc_functions # external useful functions for general use

for f in ~/.zshrc_*; do
  echo source $f;
done


################################################################################
############################### GENERAL ALIASES ################################
################################################################################
alias reloadrc="source ~/.zshrc"
alias 'ps?'="ps ax | grep"
alias gscrn='~/scripts/screenSwitcher/screenSwitcher.pl'
alias sls="screen -ls"
alias sr="screen -R"


################################################################################
############################### GENERAL SETTINGS ###############################
################################################################################
export EDITOR=nano
export LD_LIBRARY_PATH="/usr/X11R6/lib:"$LD_LIBRARY_PATH
export NYTPROF=start=no


################################################################################
##################### LOCATE PACKAGE FOR UNKNOWN COMMANDS ######################
################################################################################
# locates and displays the package info for any commands that are unknown      # 
source /etc/zsh_command_not_found

################################################################################
########################### PRE CONFIGURED SETTINGS ############################
################################################################################
setopt histignorealldups sharehistory

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

# Keep 1000 lines of history within the shell and save it to ~/.zsh_history:
HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.zsh_history



# Use modern completion system
autoload -Uz compinit
compinit

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' format 'Completing %d' # text to display at the top of the autocomplete selectoins (IDK what %d is yet, the id of what it is completeing but not how to set it)
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select=2
eval "$(dircolors -b)"
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true

zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
